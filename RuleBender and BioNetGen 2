# A simple Michaelis-Menten enzymatic reaction
# this code contains the following reactions 
#1) Ribulose-5-Phosphate  (R5P) → 3,4,Dihydroxy-2-butanone-4-phosphate (D2B4P) (enzyme is RibA) 
#2) D2B4P → 6,7-Dimethyl-8-ribityl lumazine (D8RL) (Enzyme is RibH) 
#3) D8RL → Riboflavin (Enzyme is RibE) 
#4) Riboflavin → FMN (Enzyme is RibC)  
#5) FMN → FAD   (Enzyme is RibC) 

begin parameters
      k1          1e6
      k1r         1e-4
      k2          0.1
end parameters


begin seed species
      R5P(a)                  5e-7
      RibA(a)                 2e-7
      D2B4P(a)                5e-7
      RibH(a)                 2e-7
      D8RL(a)                 5e-7
      RibE(a)                 2e-7
      Riboflavin(a)           0
      RibC(a)                 2e-7
      FMN(a)                  0
      FAD()                   0
end seed species


# the actual reactions
begin reaction rules
      # Binding and dissociation
      R5P(a) + RibA(a) <-> R5P(a!1).RibA(a!1)                           k1, k1r
      D2B4P(a) + RibH(a) <-> D2B4P(a!1).RibH(a!1)                       k1, k1r
      D8RL(a) + RibE(a) <-> D8RL(a!1).RibE(a!1)                         k1, k1r
      Riboflavin(a) + RibC(a) <-> Riboflavin(a!1).RibC(a!1)             k1, k1r
      FMN(a) + RibC(a) <-> FMN(a!1).RibC(a!1)                           k1, k1r
      # Production
      R5P(a!1).RibA(a!1) -> D2B4P(a) + RibA(a)                          k2
      D2B4P(a!1).RibH(a!1) -> D8RL(a) + RibH(a)                         k2
      D8RL(a!1).RibE(a!1) -> Riboflavin(a) + RibE(a)                    k2
      Riboflavin(a!1).RibC(a!1) -> FMN(a) + RibC(a)                     k2
      FMN(a!1).RibC(a!1) -> FAD() + RibC(a)                             k2
end reaction rules


begin observables
      Molecules   RibA                    RibA(a)
      Molecules   R5P                     R5P(a)
      Molecules   D2B4P                   D2B4P(a)
      Molecules   R5P_RibA                R5P(a!1).RibA(a!1)
      Molecules   RibH                    RibH(a)
      Molecules   D8RL                    D8RL(a)
      Molecules   D2B4P_RibH              D2B4P(a!1).RibH(a!1)
      Molecules   RibE                    RibE(a)
      Molecules   Riboflavin              Riboflavin(a)
      Molecules   D8RL_RibE               D8RL(a!1).RibE(a!1)
      Molecules   RibC                    RibC(a)
      Molecules   FMN                     FMN(a)
      Molecules   Riboflavin_RibC         Riboflavin(a!1).RibC(a!1)
      Molecules   FMN_RibC                FMN(a!1).RibC(a!1)
      Molecules   FAD                     FAD()
end observables

## actions ##
generate_network({overwrite=>1})

# Equilibration
simulate_ode({t_end=>100,n_steps=>1000,atol=>1e-10,rtol=>1e-8,sparse=>1})
